import de.reiss.bible2net.theword.ApkSignatureLoader

apply plugin: "com.android.application"
apply plugin: 'io.fabric'
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

android {

    compileSdkVersion sdk.compile
    buildToolsVersion versions.build_tools

    defaultConfig {
        applicationId "de.reiss.bible2net.theword"

        minSdkVersion sdk.min

        targetSdkVersion sdk.target

        versionCode isIdeBuild ? 1 : app_version_code
        versionName app_version_name

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "de.reiss.bible2net.theword.testutil.AppTestRunner"
    }

    signingConfigs {
        release {
            def propertiesFile = new File('apk_signature.properties')
            def apkSignatureLoader = new ApkSignatureLoader()
            def apkSignature = apkSignatureLoader.loadApkSignature(propertiesFile)
            if (apkSignature != null) {
                storeFile = apkSignature.storeFile
                storePassword = apkSignature.storePassword
                keyAlias = apkSignature.keyAlias
                keyPassword = apkSignature.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            ext.enableCrashlytics = false
        }
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    implementation project(":database")
    implementation project(":logger")
    implementation project(":downloader")
    implementation project(":twdparser")
    implementation project(":events")

    implementation "com.android.support:appcompat-v7:$versions.support_lib"
    implementation "com.android.support:design:$versions.support_lib"
    implementation "com.android.support:recyclerview-v7:$versions.support_lib"
    implementation "com.android.support:cardview-v7:$versions.support_lib"

    implementation "com.takisoft.fix:preference-v7:28.0.0.0"
    implementation "com.takisoft.fix:preference-v7-extras:28.0.0.0"

    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    kapt "android.arch.lifecycle:compiler:1.1.1"

    implementation "com.google.android.gms:play-services-gcm:17.0.0"

    implementation "com.google.dagger:dagger:2.25.2"
    kapt "com.google.dagger:dagger-compiler:2.25.2"
    compileOnly "org.glassfish:javax.annotation:10.0-b28"

    implementation("com.crashlytics.sdk.android:crashlytics:2.9.5@aar") {
        transitive = true
    }

    testImplementation "junit:junit:4.12"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.6.0"
    testImplementation "android.arch.core:core-testing:1.1.1"

    androidTestImplementation "com.android.support.test:runner:1.0.2.support_test"
    androidTestImplementation "com.android.support.test:rules:1.0.2.support_test"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"
    androidTestImplementation "org.mockito:mockito-android:3.1.0"
    androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.6.0"

}
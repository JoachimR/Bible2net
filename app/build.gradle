import de.reiss.bible2net.theword.ApkSignatureLoader

apply plugin: "com.android.application"
apply plugin: 'io.fabric'
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

android {

    compileSdkVersion sdk.compile
    buildToolsVersion versions.build_tools

    defaultConfig {
        applicationId "de.reiss.bible2net.theword"

        minSdkVersion sdk.min

        targetSdkVersion sdk.target

        versionCode isIdeBuild ? 1 : app_version_code
        versionName app_version_name

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "de.reiss.bible2net.theword.testutil.AppTestRunner"
    }

    signingConfigs {
        release {
            def propertiesFile = new File('apk_signature.properties')
            def apkSignatureLoader = new ApkSignatureLoader()
            def apkSignature = apkSignatureLoader.loadApkSignature(propertiesFile)
            if (apkSignature != null) {
                storeFile = apkSignature.storeFile
                storePassword = apkSignature.storePassword
                keyAlias = apkSignature.keyAlias
                keyPassword = apkSignature.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            ext.enableCrashlytics = false
        }
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    implementation libraries.kotlin.lib

    implementation project(":database")
    implementation project(":logger")
    implementation project(":downloader")
    implementation project(":twdparser")
    implementation project(":events")

    implementation libraries.support.app_compat
    implementation libraries.support.design
    implementation libraries.support.recycler_view
    implementation libraries.support.card_view

    implementation libraries.preference_fix.core
    implementation libraries.preference_fix.extras

    implementation libraries.arch.runtime
    implementation libraries.arch.extensions
    kapt libraries.arch.compiler

    implementation libraries.play_services.gcm

    implementation libraries.dagger.core
    kapt libraries.dagger.compiler
    provided libraries.dagger.glassfish

    implementation(libraries.crashlytics) {
        transitive = true
    }

    testImplementation libraries.junit
    testImplementation libraries.mockito_kotlin
    testImplementation libraries.arch.core_testing

    androidTestImplementation libraries.support.test_runner
    androidTestImplementation libraries.support.test_rules
    androidTestImplementation libraries.espresso.core
    androidTestImplementation libraries.espresso.contrib
    androidTestImplementation libraries.espresso.intents
    androidTestImplementation libraries.mockito_android
    androidTestImplementation libraries.mockito_kotlin

}
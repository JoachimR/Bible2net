import de.reiss.bible2net.theword.ApkSignatureLoader

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

android {
    namespace "de.reiss.bible2net.theword"

    compileSdk 35

    defaultConfig {
        applicationId "de.reiss.bible2net.theword"

        minSdk 26

        targetSdk 35

        versionCode isIdeBuild ? 1 : app_version_code
        versionName app_version_name

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "de.reiss.bible2net.theword.testutil.AppTestRunner"
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    signingConfigs {
        release {
            def propertiesFile = new File('apk_signature.properties')
            def apkSignatureLoader = new ApkSignatureLoader()
            def apkSignature = apkSignatureLoader.loadApkSignature(propertiesFile)
            if (apkSignature != null) {
                storeFile = apkSignature.storeFile
                storePassword = apkSignature.storePassword
                keyAlias = apkSignature.keyAlias
                keyPassword = apkSignature.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            ext.enableCrashlytics = false
        }
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.0.21"

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.github.kizitonwose.colorpreference:support:1.1.0'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.8.7'

    implementation 'androidx.work:work-runtime-ktx:2.10.0'

    implementation 'com.google.dagger:dagger-android:2.52'
    implementation 'com.google.dagger:dagger-android-support:2.52'
    kapt 'com.google.dagger:dagger-android-processor:2.52'
    kapt 'com.google.dagger:dagger-compiler:2.52'

    api "org.greenrobot:eventbus:3.3.1"

    api 'androidx.room:room-runtime:2.6.1'
    api 'androidx.room:room-ktx:2.6.1'
    kapt 'androidx.room:room-compiler:2.6.1'

    api "com.squareup.okhttp3:okhttp:4.12.0"
    api "com.squareup.okhttp3:logging-interceptor:4.12.0"
    api "com.squareup.retrofit2:retrofit:2.11.0"
    api "com.squareup.retrofit2:converter-moshi:2.11.0"
    api "com.squareup.moshi:moshi:1.15.1"
    api "com.squareup.moshi:moshi-adapters:1.15.1"
    api "com.squareup.moshi:moshi-kotlin:1.15.1"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.15.1"

    implementation "org.jsoup:jsoup:1.18.1"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"
    testImplementation 'androidx.arch.core:core-testing:2.2.0'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.6.1'
    androidTestImplementation "org.mockito:mockito-android:5.14.2"
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"
}